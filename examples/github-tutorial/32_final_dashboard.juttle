input cat_in: select -items [
  { value: 'Dev', label: 'Dev' },
  { value: 'Deploy', label: 'Deploy' },
  { value: 'Monitor', label: 'Monitor' }
  ]
  -label 'Category'
  -default 'Dev';

input type_in: select -items [
  { value: 'IssuesEvent', label: 'Issues' },
  { value: 'ForkEvent', label: 'Forks' },
  { value: 'PushEvent', label: 'Pushes' },
  { value: 'Event', label: 'Any' }
  ]
  -label 'Event Type'
  -default 'Event';

read elastic -from :2015-01-01: -to :2015-07-01: category = cat_in AND type ~ '*${type_in}*'
|(
  reduce count()
  | view tile -title 'GitHub events count (${cat_in}, ${type_in})' -row 0 -col 0;

  reduce count() by repo_name
  | sort count -desc
  | head 10
  | view table -title 'GitHub events for top 10 repos (${cat_in}, ${type_in})' -row 0 -col 1;

  reduce -from :2015-01-01: -over :w: -every :d: count() by repo_name
  | view timechart -keyField 'repo_name' -title 'Rolling count of GitHub events (${cat_in}, ${type_in})' -row 1 -col 0;
)

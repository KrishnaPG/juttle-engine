import "../common/capacity_planning_cpu.juttle" as capacity_cpu;

capacity_cpu.cpu_piechart -cw_product 'EC2';

// Also display a pie chart of EC2 instance types scaled by hourly
// cost. These costs are accurate as of 2016-02-17.
const instance_prices = {
    'm3.medium': 0.0087,
    'm1.small': 0.0081,
    'm3.large': 0.133,
    'm1.medium': 0.0133,
    't1.micro': 0.0031,
    'c3.xlarge': 0.02,
    't2.micro': 0.013,
    'c3.8xlarge': 1.68,
    'm4.4xlarge': 0.958,
    'm3.2xlarge': 0.532,
    'c3.large': 0.105,
    'c4.2xlarge': 0.419,
    'm3.xlarge': 0.266,
    't2.small': 0.026,
    't2.medium': 0.05,
};

// Take categories contributing less than x percent to the total and
// group them together in an "Other" category.
sub threshold(thresh) {
    (reduce total=sum(cost); pass)
    | join
    | put pct = 100.0*cost/total
    | put name=(pct >= thresh ? name : 'other')
    | reduce cost=sum(cost), time=last(time) by name
    | sort name
}

read mysql -table 'aws_aggregation' -last :10m:
    product='EC2' AND demographic='EC2 Instance Type'
    | put cost=value*instance_prices[name]
    | batch -every :1s:
    | threshold -thresh 2
    | view piechart -valueField "cost" -title "EC2 Instances Scaled by Hourly Cost" -categoryField "name";




